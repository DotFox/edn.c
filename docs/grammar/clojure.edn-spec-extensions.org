#+title: clojure.edn Spec Extensions

* WhiteSpace

Additional characters treated as whitespace: (~\u000B~, ~\u000C~, ~\u001C~, ~\u001D~, ~\u001E~, ~\u001F~)

#+begin_src plantuml :file ./clojure.edn_whitespace_ebnf.svg :exports results
@startebnf
!theme amiga

WhiteSpace = " "      (* U+0020 SPACE *)
           | "\t"     (* U+0009 HORIZONTAL TABULATION *)
           | "\n"     (* U+000A LINE FEED *)
           | "\u000B" (* U+000B VERTICAL TABULATION *)
           | "\f"     (* U+000C FORM FEED *)
           | "\r"     (* U+000D CARRIAGE RETURN *)
           | "\u001C" (* U+001C FILE SEPARATOR *)
           | "\u001D" (* U+001D GROUP SEPARATOR *)
           | "\u001E" (* U+001E RECORD SEPARATOR *)
           | "\u001F" (* U+001F UNIT SEPARATOR *)
           | ","      (* U+002C COMMA *)
           | Comment
           ;
@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_whitespace_ebnf.svg]]

* Additional elements

#+begin_src plantuml :file ./clojure.edn_elements_ebnf.svg :exports results
@startebnf
!theme amiga

EdnElement = Nil
           | Boolean
           | Symbolic         (* ##Inf, ##-Inf, and ##NaN *)
           | Character        (* Additional named characters and octal form *)
           | String           (* Additional octal escape sequence *)
           | Integer          (* Additional integer forms and minor deviations *)
           | Float
           | Ratio            (* Rational ratios *)
           | Symbol           (* Many small additions and deviations *)
           | Keyword          (* Same as Symbols *)
           | List
           | Vector
           | Map              (* Additional syntax for namespaced maps *)
           | Set
           | TaggedElement
           | DiscardSequence
           | MetadataSequence (* Metadata literal for some Elements *)
           ;
@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_elements_ebnf.svg]]

** Symbolic values

#+begin_src plantuml :file ./clojure.edn_symbolic_ebnf.svg :exports results
@startebnf
!theme amiga

Symbolic = "##Inf" | "##-Inf" | "##NaN" ;

@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_symbolic_ebnf.svg]]

** Characters

Additional named characters (\backspace, \formfeed) and octal form.

#+begin_src plantuml :file ./clojure.edn_characters_ebnf.svg :exports results
@startebnf
!theme amiga

Character = "\" , ( CharLiteral | CharName | UnicodeEscape | OctalEscape ) ;

CharLiteral = ? Any non-whitespace character ? ;

CharName = "newline" | "space" | "tab" | "return" | "backspace" | "formfeed" ;

UnicodeEscape = "u" , HexDigit , HexDigit , HexDigit , HexDigit ;

OctalEscape = "o", Octal ;

Octal =  ( ( OctalDigit , [ OctalDigit ] ) | ( ZeroToThree , 2 * OctalDigit ) ) ;

OctalDigit = ? 0-7 ? ;

ZeroToThree = ? 0-3 ? ;

HexDigit = ? 0-9, a-z, A-Z ? ;
@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_characters_ebnf.svg]]

** Strings

Additional escape chars, Octal (Legacy Java Feature) and Unicode Escape Sequences (also kinda Legacy Java Feature).

#+begin_src plantuml :file ./clojure.edn_strings_ebnf.svg :exports results
@startebnf
!theme amiga

String = '"' , { StringCharacter } , '"' ;

StringCharacter = InputCharacter
                | EscapeSequence ;

InputCharacter = ? All Unicode Characters ? - ( '\' | '"' ) ;

EscapeSequence = '\' , ( EssentialEscape | Octal | UnicodeEscape ) ;

EssentialEscape = ( 't' | 'r' | 'n' | 'b' | 'f' | '\' | '"' ) ;
@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_strings_ebnf.svg]]

** Integers

Octal, hex and arbitrary radix forms.

#+begin_src plantuml :file ./clojure.edn_integers_ebnf.svg :exports results
@startebnf
!theme amiga

Integer = [ "-" | "+" ] , IntegerForm ;

IntegerForm = DecimalInteger
            | OctalInteger
            | HexadecimalInteger
            | RadixInteger ;

DecimalInteger = ( ( ? Decimal digit ? - "0" , { ? Decimal digit ? } ) | "0" ) , [ "N" ] ;

OctalInteger = "0" , Octal , [ "N" | "M" ] ;

HexadecimalInteger = "0x" , { HexDigit }- , [ "N" ] ;

RadixInteger = Radix , "r" , { RadixDigit (* must be consistent with the radix basis. *) }- ;

Radix = OneToNine
      | TenToTwentyNine
      | ThirtyToThirtySix ;

OneToNine = ? Decimal digit ? - "0" ;

TenToTwentyNine = ( "1" | "2" ) , ? Decimal digit ? ;

ThirtyToThritySix = "3" , ? Decimal digit ? - ( "7" | "8" | "9" ) ;

RadixDigit = ? 0-9, a-z, A-Z ? ;

@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_integers_ebnf.svg]]

** Rational ratios

#+begin_src plantuml :file ./clojure.edn_rational_ratios_ebnf.svg :exports results
@startebnf
!theme amiga

Ratio = [ "-" | "+" ] , DecimalInteger , "/" , DecimalInteger - "0" ;

@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_rational_ratios_ebnf.svg]]

** Symbols

** Keywords

** Maps

Map literals can optionally specify a default namespace context for keys in the map using a ~#:ns~ prefix.

#+begin_src plantuml :file ./clojure.edn_namespaced_map_syntax_ebnf.svg :exports results
@startebnf
!theme amiga

Map = [ "#:" , KeywordNamespace ] , "{" , { 2 * ( { WhiteSpace } , EdnElement ) } , "}" ;

@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_namespaced_map_syntax_ebnf.svg]]

** MetadataSequences

The metadata reader macro first reads the metadata and attaches it to the next form read. Metadata can be chained in which case they are merged from right to left.

#+begin_src plantuml :file ./clojure.edn_metadata_sequence_ebnf.svg :exports results
@startebnf
!theme amiga

MetadataSequence = "^" , { WhiteSpace } , Metadata , { WhiteSpace } , MetadataCarrier ;

Metadata = Map
         | String
         | SimpleSymbol
         | ( "[" , { WhiteSpace | SimpleSymbol } , "]" ) ;

MetadataCarrier = Symbol
                | List
                | Vector
                | Set
                | Map
                | TaggedElement (* when it resolve to another metadata carrier type *) ;

@endebnf
#+end_src

#+RESULTS:
[[file:./clojure.edn_metadata_sequence_ebnf.svg]]
