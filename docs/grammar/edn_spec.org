#+title: Edn Spec

Source: https://github.com/edn-format/edn

Copyright: Â© Rich Hickey and contributors.

Modifications: This document is a derivative of the original EDN specification. It has been modified by EDN.c project to include a formal EBNF grammar.

-----

* Rationale

~edn~ is an extensible data notation. A superset of ~edn~ is used by Clojure to represent programs, and it is used by Datomic and other applications as a data transfer format. This spec describes ~edn~ in isolation from those and other specific use cases, to help facilitate implementation of readers and writers in other languages, and for other uses.

~edn~ supports a rich set of built-in elements, and the definition of extension elements in terms of the others. Users of data formats without such facilities must rely on either convention or context to convey elements not included in the base set. This greatly complicates application logic, betraying the apparent simplicity of the format. ~edn~ is simple, yet powerful enough to meet the demands of applications without convention or complex context-sensitive logic.

~edn~ is a system for the conveyance of values. It is not a type system, and has no schemas. Nor is it a system for representing objects - there are no reference types, nor should a consumer have an expectation that two equivalent elements in some body of ~edn~ will yield distinct object identities when read, unless a reader implementation goes out of its way to make such a promise. Thus the resulting values should be considered immutable, and a reader implementation should yield values that ensure this, to the extent possible.

~edn~ is a set of definitions for acceptable elements. A use of ~edn~ might be a stream or file containing elements, but it could be as small as the conveyance of a single element in e.g. an HTTP query param.

There is no enclosing element at the top level. Thus ~edn~ is suitable for streaming and interactive applications.

The base set of elements in ~edn~ is meant to cover the basic set of data structures common to most programming languages. While ~edn~ specifies how those elements are formatted in text, it does not dictate the representation that results on the consumer side. A well behaved reader library should endeavor to map the elements to programming language types with similar semantics.

* General considerations

edn elements, streams and files should be encoded using [[http://en.wikipedia.org/wiki/UTF-8][UTF-8]].

Elements are generally separated by whitespace. Whitespace, other than within strings, is not otherwise significant, nor need redundant whitespace be preserved during transmissions. Commas ~,~ are also considered whitespace, other than within strings.

#+name: edn_document
#+begin_src ebnf :eval no :exports code
Edn = { Spacing | EdnElement | ? End of input ? }- ;

Spacing = { WhiteSpace | Comment | "," }- ;

WhiteSpace = " " | "\t" | "\n" | "\r" ;

Comment = ";" , { ( ? Any character ? - LineEnd ) } , LineEnd ;

LineEnd = "\n" | "\r" | ? End of input ? ;
#+end_src

#+begin_src plantuml :file edn_document.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_document>>
@endebnf
#+end_src

#+RESULTS:
[[file:edn_document.svg]]

* Built-in elements

#+name: edn_elements
#+begin_src ebnf :eval no :exports code
EdnElement = Character
           | String
           | Float
           | Integer
           | Symbol
           | Nil
           | Boolean
           | Keyword
           | List
           | Vector
           | Map
           | Set
           | TaggedElement
           | DiscardSequence ;
#+end_src

#+begin_src plantuml :file edn_elements.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_elements>>
@endebnf
#+end_src

#+RESULTS:
[[file:edn_elements.svg]]

** Nil

Represents nil, null or nothing. It should be read as an object with similar meaning on the target platform.

#+name: edn_nil
#+begin_src ebnf :eval no :exports code
Nil = "nil" ;
#+end_src

#+begin_src plantuml :file ./edn_nil.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_nil>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_nil.svg]]

** Booleans

If a platform has canonic values for true and false, it is a further semantic of booleans that all instances of ~true~ yield that (identical) value, and similarly for ~false~.

#+name: edn_boolean
#+begin_src ebnf :eval no :exports code
Boolean = "true" | "false" ;
#+end_src

#+begin_src plantuml :file ./edn_boolean.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_boolean>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_boolean.svg]]

** Characters

#+name: edn_character
#+begin_src ebnf :eval no :exports code
Character = "\" , ( CharLiteral | CharName | UnicodeEscape ) ;

CharLiteral = ? Any character ? - WhiteSpace ;

CharName = "newline" | "space" | "tab" | "return" ;

UnicodeEscape = "u" , HexDigit , HexDigit , HexDigit , HexDigit ;

HexDigit = ? 0-9, a-z, A-Z ? ;
#+end_src

#+begin_src plantuml :file ./edn_character.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_character>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_character.svg]]

** Strings

#+name: edn_string
#+begin_src ebnf :eval no :exports code
String = '"' , { StringCharacter } , '"' ;

StringCharacter = InputCharacter
                | EscapeSequence ;

InputCharacter = ? Any character ? - ( "\" | '"' ) ;

EscapeSequence = "\" , ( "t" | "r" | "n" | "\" | '"' ) ;
#+end_src

#+begin_src plantuml :file ./edn_string.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_string>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_string.svg]]

** Symbols

Symbols are used to represent identifiers, and should map to something other than strings, if possible.

#+name: edn_symbol
#+begin_src ebnf :eval no :exports code
Symbol = Identifier - ( Nil | Boolean ) ;

Identifier = IdentifierPrefix , "/" , IdentifierName
           | IdentifierPrefix
           | "/" ;

IdentifierPrefix = ( IdentifierStart - ( "+" | "-" | "." ) ) , { IdentifierBody }
                 | ( "+" | "-" | "." ) , [ ( IdentifierBody - ? Decimal digit ? ) , { IdentifierBody } ];

IdentifierName = { IdentifierStart }- ;

IdentifierStart = IdentifierConstituent - ( ? Decimal digit ? ) ;

IdentifierBody = IdentifierConstituent
               | "#"
               | ":" ;

IdentifierConstituent = ? Alphanumeric ?
                      | "."
                      | "*"
                      | "+"
                      | "!"
                      | "-"
                      | "_"
                      | "?"
                      | "$"
                      | "%"
                      | "&"
                      | "="
                      | "<"
                      | ">" ;
#+end_src

#+begin_src plantuml :file ./edn_symbol.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_symbol>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_symbol.svg]]

** Keywords

Keywords are identifiers that typically designate themselves. They are semantically akin to enumeration values.

If the target platform supports some notion of interning, it is a further semantic of keywords that all instances of the same keyword yield the identical object.

If the target platform does not have a keyword type distinct from a symbol type, the same type can be used without conflict, since the mandatory leading ~:~ of keywords is disallowed for symbols.

#+name: edn_keyword
#+begin_src ebnf :eval no :exports code
Keyword = ":" , Identifier - "/" ;
#+end_src

#+begin_src plantuml :file ./edn_keyword.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_keyword>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_keyword.svg]]

** Integers

64-bit (signed integer) precision is expected. An integer can have the suffix ~N~ to indicate that arbitrary precision is desired. ~-0~ is a valid integer not distinct from ~0~.

#+name: edn_integer
#+begin_src ebnf :eval no :exports code
Integer = [ "-" | "+" ] , ( ( ? Decimal digit ? - "0" , { ? Decimal digit ? } ) | "0" ) , [ "N" ] ;
#+end_src

#+begin_src plantuml :file ./edn_integer.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_integer>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_integer.svg]]

** Floating point numbers

64-bit (double) precision is expected.

#+name: edn_float
#+begin_src ebnf :eval no :exports code
Float = [ "-" | "+" ] , ( ( ? Decimal digit ? - "0" , { ? Decimal digit ? } ) | "0" ) , ( Fractional , [ Exponent ] , [ "M" ] | [ Fractional ] , Exponent , [ "M" ] | [ Fractional ] , [ Exponent ] , "M" ) ;

Fractional = "." , { ? Decimal digit ? } ;

Exponent = ( "e" | "E" ) , [ "-" | "+" ] , ? Decimal digit ? , { ? Decimal digit ? } ;
#+end_src

#+begin_src plantuml :file ./edn_float.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_float>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_float.svg]]

** Lists

A list is a sequence of values. Note that lists can be heterogeneous.

#+name: edn_list
#+begin_src ebnf :eval no :exports code
List = "(" , { Spacing | EdnElement } , ")" ;
#+end_src

#+begin_src plantuml :file ./edn_list.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_list>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_list.svg]]

** Vectors

A vector is a sequence of values that supports random access. Note that vectors can be heterogeneous.

#+name: edn_vector
#+begin_src ebnf :eval no :exports code
Vector = "[" , { Spacing | EdnElement } , "]" ;
#+end_src

#+begin_src plantuml :file ./edn_vector.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_vector>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_vector.svg]]

** Maps

A map is a collection of associations between keys and values. Each key should appear at most once. No semantics should be associated with the order in which the pairs appear.

#+name: edn_map
#+begin_src ebnf :eval no :exports code
Map = "{" , { Spacing | MapEntry } , "}" ;

MapEntry = EdnElement , [ Spacing ] , EdnElement ;
#+end_src

#+begin_src plantuml :file ./edn_map.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_map>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_map.svg]]

** Sets

A set is a collection of unique values. No semantics should be associated with the order in which the elements appear. Note that sets can be heterogeneous.

#+name: edn_set
#+begin_src ebnf :eval no :exports code
Set = "#{" , { Spacing | EdnElement } , "}" ;
#+end_src

#+begin_src plantuml :file ./edn_set.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_set>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_set.svg]]

** Tagged elements

~edn~ supports extensibility through a simple mechanism. A tag indicates the semantic interpretation of the following element. It is envisioned that a reader implementation will allow clients to register handlers for specific tags. Upon encountering a tag, the reader will first read the next element (which may itself be or comprise other tagged elements), then pass the result to the corresponding handler for further interpretation, and the result of the handler will be the data value yielded by the tag + tagged element, i.e. reading a tag and tagged element yields one value. This value is the value to be returned to the program and is not further interpreted as edn data by the reader.

This process will bottom out on elements either understood or built-in.

Thus you can build new distinct readable elements out of (and only out of) other readable elements, keeping extenders and extension consumers out of the text business.

The semantics of a tag, and the type and interpretation of the tagged element are defined by the steward of the tag.

If a reader encounters a tag for which no handler is registered, the implementation can either report an error, call a designated 'unknown element' handler, or create a well-known generic representation that contains both the tag and the tagged element, as it sees fit. Note that the non-error strategies allow for readers which are capable of reading any and all ~edn~, in spite of being unaware of the details of any extensions present.

Tag symbols without a prefix are reserved by ~edn~ for built-ins defined using the tag system.

User tags must contain a prefix component, which must be owned by the user (e.g. trademark or domain) or known unique in the communication context.

A tag may specify more than one format for the tagged element, e.g. both a string and a vector representation.

Tags themselves are not elements. It is an error to have a tag without a corresponding tagged element.

#+name: edn_tagged
#+begin_src ebnf :eval no :exports code
TaggedElement = "#" , Tag , [ Spacing ] , EdnElement ;

Tag = TagPrefix , "/" , IdentifierName
    | TagPrefix ;

TagPrefix = ? Alpha ? , { IdentifierBody } ;

(* built-in tagged elements *)

Instant = "#inst" , [ Spacing ] , ? RFC-3339 formatted string ? ;

UUID = "#uuid" , [ Spacing ] , ? Canonical UUID string representation ? ;
#+end_src

#+begin_src plantuml :file ./edn_tagged.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_tagged>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_tagged.svg]]

*** built-in tagged elements

**** #inst "rfc-3339-format"

An instant in time. The tagged element is a string in [[http://www.ietf.org/rfc/rfc3339.txt][RFC-3339]] format.

**** #inst "1985-04-12T23:20:50.52Z"

A [[http://en.wikipedia.org/wiki/Universally_unique_identifier][UUID]]. The tagged element is a canonical UUID string representation.

** Discard

Discard sequence, indicating that the next element (whether separated from ~#_~ by whitespace or not) should be read and discarded. Note that the next element must still be a readable element. A reader should not call user-supplied tag handlers during the processing of the element to be discarded.

#+name: edn_discard
#+begin_src ebnf :eval no :exports code
DiscardSequence = "#_" , [ Spacing ] , EdnElement ;
#+end_src

#+begin_src plantuml :file ./edn_discard.svg :noweb yes :exports results
@startebnf
!theme amiga
<<edn_discard>>
@endebnf
#+end_src

#+RESULTS:
[[file:./edn_discard.svg]]

* Equality

Sets and maps have requirements that their elements and keys respectively be unique, which requires a mechanism for determining when 2 values are not unique (i.e. are equal).

nil, booleans, strings, characters, and symbols are equal to values of the same type with the same ~edn~ representation.

integers and floating point numbers should be considered equal to values only of the same magnitude, type, and precision. Comingling numeric types and precision in map/set key/elements, or constituents therein, is not advised.

sequences (lists and vectors) are equal to other sequences whose count of elements is the same, and for which each corresponding pair of elements (by ordinal) is equal.

sets are equal if they have the same count of elements and, for every element in one set, an equal element is in the other.

maps are equal if they have the same number of entries, and for every key/value entry in one map an equal key is present and mapped to an equal value in the other.

tagged elements must define their own equality semantics. #uuid elements are equal if their canonic representations are equal. #inst elements are equal if their representation strings designate the same timestamp per [[http://www.ietf.org/rfc/rfc3339.txt][RFC-3339]].

* Complete EBNF Grammar

#+name: edn_ebnf
#+begin_src ebnf :eval no :noweb yes :exports results :tangle edn_grammar.ebnf
<<edn_document>>

<<edn_elements>>

<<edn_nil>>

<<edn_boolean>>

<<edn_character>>

<<edn_string>>

<<edn_symbol>>

<<edn_keyword>>

<<edn_integer>>

<<edn_float>>

<<edn_list>>

<<edn_vector>>

<<edn_map>>

<<edn_set>>

<<edn_discard>>

<<edn_tagged>>
#+end_src

#+RESULTS:
[[file:edn_grammar.ebnf]]

#+begin_src plantuml :file ./edn_ebnf.svg :noweb yes :exports results
@startebnf
!theme amiga
title EDN grammar

(* Assumes greedy repetition
   Assumes ordered alternation *)

<<edn_ebnf>>

@endebnf
#+end_src

#+RESULTS:
[[file:./edn_ebnf.svg]]
